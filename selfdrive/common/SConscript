Import('env', 'arch', 'SHARED', 'QCOM_REPLAY')

if SHARED:
  fxn = env.SharedLibrary
else:
  fxn = env.Library

common_libs = ['params.cc', 'swaglog.cc', 'util.c', 'gpio.cc', 'i2c.cc']

_common = fxn('common', common_libs, LIBS="json11")

files = [
  'clutil.c',
  'cqueue.c',
  'glutil.c',
  'visionimg.cc',
]

if arch == "aarch64":
  defines = {}
  files += [
    'framebuffer.cc',
    'touch.c',
  ]
  _gpu_libs = ['gui', 'adreno_utils']
elif arch == "larch64":
  defines = {"CLU_NO_CACHE": None}
  _gpu_libs = ['GL']
else:
  defines = {"CLU_NO_CACHE": None}
  _gpu_libs = ["GL"]

_gpucommon = fxn('gpucommon', files, CPPDEFINES=defines, LIBS=_gpu_libs)
Export('_common', '_gpucommon', '_gpu_libs')
